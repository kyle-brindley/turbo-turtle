#! /usr/bin/env python

import pathlib

# Inherit the parent construction environment
Import("env")

# Limit list of source files to allow Conda build-test to test off the installed package
pytest_source_list = [
    "pyproject.toml",
]

pytest_command = "PYTHONDONTWRITEBYTECODE=1 pytest --junitxml=${TARGET.abspath}"
coverage_command = "coverage xml -o ${TARGETS[1].abspath}"
targets = env.Command(
    target=["test_results.xml", "coverage.xml"],
    source=pytest_source_list,
    action=[
        "${pytest_command} -vvv -n 4 -m 'not systemtest' --cov",
        "${coverage_command}"
    ],
    pytest_command=pytest_command,
    coverage_command=coverage_command
)
env.Alias("pytest", targets)
env.Alias("regression", targets)
# Always run pytests in place of a complete source list
env.AlwaysBuild(targets)

# Cubit unit tests
for name, cubit_environment in env["cubit_environments"].items():
    targets = cubit_environment.Command(
        target=[f"test_{name}_results.xml"],
        source=pytest_source_list + ["turbo_turtle/tests/test_cubit_python.py"],
        action=[
            "${pytest_command} ${SOURCES[1].abspath} -vvv -n 4 -m 'not systemtest' --cov",
        ],
        pytest_command=pytest_command,
    )
    cubit_environment.Alias("test_cubit_python", targets)
    cubit_environment.Alias("regression", targets)
    # Always run pytests in place of a complete source list
    cubit_environment.AlwaysBuild(targets)

source = pytest_source_list + [str(pathlib.Path("turbo_turtle/tests/test_system.py"))]
targets = env.Command(
    target=["systemtest_results.xml"],
    source=source,
    action=[
        "${pytest_command} -v --no-showlocals -n 4 -m systemtest --tb=short --cache-clear"
    ],
    pytest_command=pytest_command
)
env.Alias("systemtest", targets)
env.Alias("systemtests", targets)
env.Alias("regression", targets)
env.AlwaysBuild(targets)

# Abaqus unit tests
for name, path in env["abaqus_matrix"].items():
    source = [
        "turbo_turtle/_abaqus_python/turbo_turtle_abaqus/test_abaqus_utilities.py",
        "turbo_turtle/_abaqus_python/turbo_turtle_abaqus/test_mixed_utilities.py",
        "turbo_turtle/_abaqus_python/turbo_turtle_abaqus/test_parsers.py",
        "turbo_turtle/_abaqus_python/turbo_turtle_abaqus/test_vertices.py",
    ]
    targets = env.Command(
        target=[f"test_{name}_python.txt"],
        source=source,
        action=[
            "PYTHONDONTWRITEBYTECODE=1 ${program} python -m unittest discover ${SOURCE.dir.abspath} --verbose 2>&1 | tee ${TARGET.abspath}"
        ],
        program=path
    )
    env.Alias("test_abaqus_python", targets)
    env.Alias("regression", targets)
    # Always run pytests in place of a complete source list
    env.AlwaysBuild(targets)
